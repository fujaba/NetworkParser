// clean --refresh-dependencies build
// -PReleaseBuild 
apply plugin: 'java'
apply plugin: 'maven'

// MAJOR VERSION - Manually set
//----------------------
ext{
	majorNumber = 4
}
//----------------------

ant.importBuild 'buildAnt.xml'
ant.importBuild 'gource.xml'
ant.importBuild 'Copyright.xml'
ant.importBuild 'ikvm.xml'
gource.group "Git"
makefilm.group "Git"
ikvm.group "Git"
AddCopyRightToSource.group "Build"

apply from: 'gradle/version.gradle'
apply from: 'gradle/tasks.gradle'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply plugin: 'idea'

apply plugin: "com.github.kt3k.coveralls"
apply from: 'gradle/mavencentral.gradle'
//apply from: 'gradle/artifactory.gradle'
apply plugin: "com.moowork.node"

group = "de.uniks"

idea {
	project {
		// jdkName = '1.7'
		// languageLevel = '1.7'
	}
}

repositories {
	jcenter()
	mavenCentral()
}
test {
	// enable TestNG support (default is JUnit)
	useJUnit {
		dependencies {
			compile(group: 'junit', name: 'junit', version: '[4,)')
		}
	}
}

buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '[2,)')
		classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:[2,)"
		classpath 'com.moowork.gradle:gradle-node-plugin:0.9'
	}
}
dependencies {
	compile group: 'org.webjars', name: 'jquery', version: '2.1.4'
}

artifacts { 
	archives buildCoreJar16, buildCoreJar17, buildCoreJar18, buildSourceJar, buildMinCoreJar, buildJavadoc
}

test {
	testLogging {
		events "failed"
		exceptionFormat "short"
		showStackTraces	true
		showStandardStreams true
		showCauses true
	}
}
task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}
jacoco.toolVersion = "0.7.+"

jacocoTestReport {
	group = "Reporting"
	description = "Generate Jacoco coverage reports after running tests."
	executionData = files("${buildDir}/jacoco/test.exec")
	reports {
		xml {
			enabled = true
			//Following value is a file
			//destination = new File("${buildDir}/jacoco/jacoco.xml")
			//build/reports/jacoco/test/jacocoTestReport.xml
		}
		csv{
			destination = new File("${buildDir}/jacoco/report.csv")
			enabled = true
		}
		html {
			enabled = true
			//Following value is a folder
			destination = new File("${buildDir}/jacoco/html")
		}
	}
	afterEvaluate {
		classDirectories = files(classDirectories.files.collect {
			fileTree(dir: it, exclude: ['**/javafx/**'])
		})
	}
}

if (project.hasProperty('branch')) {
	new ProcessBuilder("git", "checkout", findProperty('branch')).start()
}

test.finalizedBy jacocoTestReport


sourceCompatibility = 1.8
targetCompatibility = 1.8

defaultTasks 'test', 'jacocoTestReport', 'buildJavadoc'
node {
	version = '0.12.2'
	npmVersion = '2.7.5'
	download = false
}

task npmCacheConfig(type: NpmTask) {
    description = "Configure the NPM cache"
    def npmCacheDir = "${gradle.getGradleUserHomeDir()}/caches/npm"
    outputs.files file(npmCacheDir)
    args = [ 'config', 'set', 'cache', npmCacheDir ]
}
task npmPackages(type: NpmTask, dependsOn: npmCacheConfig) {
    description = "Install Node.js packages"
    args = [ 'install' ]
    inputs.files file('package.json')
    outputs.files file('node_modules')
}
